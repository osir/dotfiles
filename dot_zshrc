# ==============================================================================
# ZSH SETTINGS
# ==============================================================================

typeset -U path PATH
path=(~/bin ~/.local/bin $path)
export PATH
fpath=( "$HOME/.config/zsh" $fpath )


# Completions
autoload -Uz compinit
zmodload zsh/complist
compinit
_comp_options+=(globdots)
zstyle :compinstall filename "$HOME/.zshrc"
zstyle ':completion:*' completer _extensions _complete _approximate

# Display colorful tags
zstyle ':completion:*:*:*:*:descriptions' format '%F{green}-- %d --%f'
zstyle ':completion:*:*:*:*:corrections' format '%F{yellow}!- %d (errors: %e) -!%f'
zstyle ':completion:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:warnings' format ' %F{red}-- no matches found --%f'
# Make group names appear in the correct place
zstyle ':completion:*' group-name ''
# Detailed file listings (a la `ls --list`)
zstyle ':completion:*' file-list all
# Colorful file listings (a la `ls --color=auto`) # TODO doesn't work?
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
# For directory commands (e.g. mkdir, cd) make - complete dir stack entries
zstyle ':completion:*' complete-options true
# If no match, try the other case
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}'
# If no match, try partial completion
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'

# Enable chaching of completions
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/.zcompcache"

# See `man zshoptions` “Completion”
setopt COMPLETE_IN_WORD LIST_PACKED AUTO_MENU LIST_ROWS_FIRST

bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'Shift+Tab' vi-up-line-or-history

# Prompt
autoload -Uz promptinit
promptinit
PURITY_CMD_MAX_EXEC_TIME=15
prompt purity

# Edit command line in $VISUAL/$EDITOR
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^v' edit-command-line

# Enable arrow keys for completion
zstyle ':completion:*' menu select
# Enable completion on privileged command lines (containing sudo)
zstyle ':completion::complete:*' gain-privileges 1

#bindkey -v
bindkey '\e[H'  beginning-of-line # Home
bindkey '\e[F'  end-of-line # End
bindkey '\e[3~' delete-char # Del

export HISTFILE=~/.histfile
export HISTSIZE=10000
export SAVEHIST=50000
export HIST_STAMPS="yyyy-mm-dd"
setopt HIST_SAVE_NO_DUPS

export HYPHEN_INSENSITIVE="true"
export ENABLE_CORRECTION="true"
export COMPLETION_WAITING_DOTS="true"

setopt AUTO_PUSHD           # Push the current directory visited on the stack.
setopt PUSHD_IGNORE_DUPS    # Do not store duplicates in the stack.
setopt PUSHD_SILENT         # Do not print the directory stack after pushd or popd.

# ==============================================================================
# ALIASES
# ==============================================================================

# directory history
alias d='dirs -v'
for index ({1..39}) alias "$index"="cd +${index}"; unset index

alias    v='nvim'
alias   se='sudoedit'
alias    r='source ranger'
alias   ra='ranger'
alias    l='\ls -lh  --color=auto --group-directories-first'
alias   ll='\ls -lAh --color=auto --group-directories-first'
alias sudi='sudo -i'
alias nmup='nmcli --ask --colors yes connection up'
alias nmdn='nmcli       --colors yes connection down'

# TODO: Default programs
export  BROWSER='/usr/bin/qutebrowser'
export   VISUAL='/usr/bin/nvim'
export   EDITOR='/usr/bin/nvim'
export    PAGER='less'
export     LANG='en_US.UTF-8'

# TODO: XDG variables

[ -f ~/.zshrc.work ] && source ~/.zshrc.work

# ==============================================================================
# FUNCTIONS
# ==============================================================================

# Creates a directory and CDs into it.
# Params: directory name
mkcd()
{
    if [[ -z "$*" ]]; then
        echo "Usage: mkcd <directory name>"
        return 1
    fi
    local dir="$*";
    mkdir -p "$dir" && cd "$dir";
}

# Starts python's http server on the given directory.
# If port is below 1025, sudo will be used.
# Params: [dir], [port]
http() {
    local back="$(pwd)"
    local port='8000'
    if [[ -n "$1" ]]; then
        cd "$1"
    fi
    if [[ -n "$2" ]]; then
        port="$2"
    fi
    if [[ $port -le 1024 ]]
    then
        sudo python3 -m http.server "$port"
    else
        python3 -m http.server "$port"
    fi
    cd "$back"
}



